#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ПоследняяВерсия") Тогда
		ПоследняяВерсия = Новый ФиксированнаяСтруктура(Параметры.ПоследняяВерсия);		
	КонецЕсли;

	Элементы.Экраны.ТекущаяСтраница = Элементы.ЭкранПроверка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ПодключитьОбработчикОжидания("_ОбновитьСостояниеНаКлиенте", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояние(Команда)

	_ОбновитьСостояниеНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОбновление(Команда)

	Установлено = _УстановитьОбновление();	
	Если Установлено Тогда
		ЗавершитьРаботуСистемы(Ложь, Истина);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Перезапустить1С(Команда)

	ЗавершитьРаботуСистемы(Ложь, Истина);

КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура _ОбновитьСостояниеНаКлиенте()

	_ОбновитьСостояниеНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура _ОбновитьСостояниеНаСервере()
	
	Обсуживание = Обработки.МИтОбслуживание.Создать();
	АктуальнаяВерсия = Новый ФиксированнаяСтруктура(Обсуживание.ПолучитьАктивнуюВерсию());	
	УстановленнаяВерсия = Новый ФиксированнаяСтруктура(Обсуживание.ПолучитьУстановленнуюВерсию());
	Если ПоследняяВерсия = Неопределено Тогда
		ПоследняяВерсия = Новый ФиксированнаяСтруктура(Обсуживание.ПолучитьПоследнююВерсию());	
	КонецЕсли;

	_ОбновитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура _ОбновитьФормуНаСервере()
	
	НужнаПерезагрузка = УстановленнаяВерсия.Версия <> АктуальнаяВерсия.Версия;
	НужнаУстановка = ПоследняяВерсия <> Неопределено И ПоследняяВерсия.Версия <> УстановленнаяВерсия.Версия;	

	Если НужнаПерезагрузка Тогда	
		Элементы.Экраны.ТекущаяСтраница = Элементы.ЭкранПерезапуск;
	ИначеЕсли НужнаУстановка Тогда
		Элементы.Экраны.ТекущаяСтраница = Элементы.ЭкранУстановка;	
	Иначе
		Элементы.Экраны.ТекущаяСтраница = Элементы.ЭкранОК;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция _УстановитьОбновление() 

	Обсуживание = Обработки.МИтОбслуживание.Создать();
    Возврат Обсуживание.УстановитьВерсию(ПоследняяВерсия.Версия);
		
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции
